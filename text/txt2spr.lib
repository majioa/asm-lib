CONVERT_TXT	PROC
	;IN DS:SI - TXT BUFFER
	;   ES:DI - SPR BUFFER
	;   CS:BX - CODE&COLOR TABLE
	;   CX - LEN OF TXT FILE
	;OUT   DX -LEN OF NEW FILE
	XOR    DX,DX
	MOV    BP,DX
CONVERT_TXT_2:
	LODSB
	CMP	AL,9H
	JZ	CONVERT_TXT_TAB
	CMP	AL,0AH
	JZ	CONVERT_TXT_ENTER
	CMP	AL,1FH
	JBE	CONVERT_TXT_1
	CALL	TAKE_COLOR
	STOSW
	INC	DX
	INC	DX
CONVERT_TXT_1:
	INC	BP
CONVERT_TXT_3:
	LOOP	CONVERT_TXT_2
	RET
CONVERT_TXT_TAB:
	CALL	SPACES_FILLING
	JMP	SHORT	CONVERT_TXT_3
CONVERT_TXT_ENTER:
	XOR	BP,BP
	JMP	SHORT	CONVERT_TXT_3
ENDP
SPACES_FILLING	PROC
	PUSH	AX
	PUSH	CX
	PUSH	BX
	MOV	BX,BP
	NEG	BX
	AND	BX,111B
	MOV	CX,BX
	OR	CX,CX
	JNZ	SPACES_FILLING_1
	MOV	CX,8
SPACES_FILLING_1:
	SHL	CX,1
	ADD	DX,CX
	SHR	CX,1
	ADD	BP,CX
	MOV	AL,' '
	POP	BX
	CALL	TAKE_COLOR
	REP	STOSW
	POP	CX
	POP	AX
	RET
ENDP
TAKE_COLOR	PROC
	;IN	AL - SYMBOL
	;	CS:BX - CODE&COLOR TABLE
	;OUT	AX - SYMBOL&COLOR
	PUSH	BX
	PUSH	DX
	MOV	DL,AL
TAKE_COLOR_1:
	MOV	AX,CS:[BX]
	INC	BX
	INC	BX
	CMP	AX,0FFFFH
	JZ	TAKE_COLOR_4
	OR	AH,AH
	JNZ	TAKE_COLOR_2
	MOV	DH,AL
	MOV	AX,CS:[BX]
	INC	BX
	INC	BX
	CMP	AL,DL
	JB	TAKE_COLOR_1
	CMP	DH,DL
	JA	TAKE_COLOR_1
	JMP	SHORT	TAKE_COLOR_3
TAKE_COLOR_4:
	MOV	AH,CS:[COLOR_DEFAULT]
	JMP	SHORT	TAKE_COLOR_3
TAKE_COLOR_2:
	CMP	AL,DL
	JNZ	TAKE_COLOR_1
TAKE_COLOR_3:
	MOV	AL,DL
	POP	DX
	POP	BX
	RET
COLOR_DEFAULT	DB	0F0H
ENDP
